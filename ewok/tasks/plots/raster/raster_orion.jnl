! -----------------------------------------------------------------------------
! Ferret script to plot FV3 fields
! Source: https://extranet.gfdl.noaa.gov/~atw/ferret/cubed_sphere/
! Modified by Benjamin Menetrier for FV3-JEDI
! -----------------------------------------------------------------------------

! Where are we?
say In raster:

! Arguments
def sym filename $1 !< File name (without the tile number and the extension)
say filename: ($filename)
let iz=$2           !< Level index
say iz: `iz`
def sym title $3    ! Figure title
say title: ($title)
let lonview=$4      ! View longitude
say lonview: `lonview`
let latview=$5      ! View latitude
say latview: `latview`
def sym colormap $6 ! Color map
say colormap: ($colormap)
let vmin=$7         ! Minimum value
say vmin: `vmin`
let vmax=$8         ! Maximum value
say vmax: `vmax`
def sym output $9   ! Output file name
say output: ($output)

! -----------------------------------------------------------------------------

! Read NetCDF file generated by raster.py
can dat/all
rep/name=s/range=1:6 (\
   use ($filename)`s`.nc;\
   let var_tmp`s`=var[k=`iz`,d=`var,r=dsetnum`];\
   let ngrd=x[gx=var_tmp`s`]+y[gy=var_tmp`s`];\
   let var`s`=reshape(var_tmp`s`,ngrd);\
   let lon`s`=lon[d=`lon,r=dsetnum`];\
   let lat`s`=lat[d=`lat,r=dsetnum`];\
   )

! Define windows
set window/aspect=1.05/size=5.0 1
ppl cross 0\
ppl color 6 100 100 100\
ppl shaset reset
palette ($colormap)

! Define levels
let nlev=30
let delta=(vmax-vmin)/nlev

! Plot cube-sphere data
go plot_cubesphere_ortho "shade/nolab/lev=(`vmin`,`vmax`,`delta`)" var lon lat "`lonview`" "`latview`"

! Define a global grid for land overlays
def ax/x=0:360:360 pcso_xdum
def ax/y=-90:90:180 pcso_ydum
def grid/x=pcso_xdum/y=pcso_ydum pcso_gdum
set grid pcso_gdum
go mp_land 7 " " " " " " " " " " 0

! Set title
!set text/font=arial
annotate/xpos=0/ypos=1.05/halign=0/valign=0/size=0.15 "($title)"

! Write file
frame/file=($output).gif
