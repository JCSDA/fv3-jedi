\can mode verify
! Usage: go plot_cubesphere_face [ face quals]
!                                [  $1   $2  ]
!
! Support script called by plot_cubesphere_*.jnl.
!
! Requires the following to be set:
!    pcsf_vals
!    pcsf_lons
!    pcsf_lats
!    deg2rad
!    pcsf_hoff
!    pcsf_voff
!    pcsf_zoom
!    pcsf_rotate_rad
!
! atw 2008aug11
! Benjamin Menetrier 2022/04/14

! Where are we?
say In plot_cubesphere_face:

! Arguments
let pcsf_face=$1
say pcsf_face: `pcsf_face`
def sym cmd $2
say cmd: ($cmd)

! Set grid to cell centers for this face
let pcsf_val=($pcsf_vals)`pcsf_face`
set grid pcsf_val

! Redefine locations to be cell vertices for this face
let mp_lambda=deg2rad*($pcsf_lons)`pcsf_face`
let mp_phi=deg2rad*($pcsf_lats)`pcsf_face`
let pcsf_rotate_rad=pcsf_rotate*deg2rad

! Render the plot
($cmd)/noax/hl=`pcsf_hoff-1/pcsf_zoom`:`pcsf_hoff+1/pcsf_zoom`/vl=`pcsf_voff-1/pcsf_zoom`:`pcsf_voff+1/pcsf_zoom` \
   pcsf_val*mp_mask[g=pcsf_val], \
   x_page*cos(pcsf_rotate_rad)-y_page*sin(pcsf_rotate_rad), \
   y_page*cos(pcsf_rotate_rad)+x_page*sin(pcsf_rotate_rad)

can var pcsf_face pcsf_val pcsf_rotate_rad

set mode/last verify
